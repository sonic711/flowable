import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.importer.XmlReportImporter
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.render.JsonReportRenderer
import com.github.jk1.license.render.SimpleHtmlReportRenderer
import com.github.jk1.license.render.XmlReportRenderer

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenLocal()
        flatDir { dirs "${rootProject.projectDir}/libs" }
    }
    dependencies {
        classpath(
                // http://github.com/jeremylong/DependencyCheck
                'org.owasp:dependency-check-gradle:10.0.2',
                // https://plugins.gradle.org/plugin/org.sonarqube
                'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929',
                // https://plugins.gradle.org/plugin/com.github.spotbugs
                'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.14',
                // https://plugins.gradle.org/plugin/io.spring.dependency-management
                'io.spring.gradle:dependency-management-plugin:1.1.5',
                // https://plugins.gradle.org/plugin/io.github.thakurvijendar.dependency-license-report
                'com.github.jk1.dependency-license-report:com.github.jk1.dependency-license-report.gradle.plugin:2.1',
                // https://plugins.gradle.org/plugin/org.hidetake.ssh
                'org.hidetake:core:2.11.2',
                // https://plugins.gradle.org/plugin/com.gorylenko.gradle-git-properties
                'com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1',
                "com.mysql:mysql-connector-j:9.0.0"
        )
    }
}

plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

group = 'com.bot'
project.version = '0.99-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

apply from: "$rootProject.projectDir/gradle/sonarqube.gradle"
apply from: "$rootProject.projectDir/gradle/dependency-check/dependency-check.gradle"

allprojects {
    apply plugin: 'project-report'
    apply plugin: 'com.github.jk1.dependency-license-report'
    htmlDependencyReport {
        projects = project.allprojects
    }

    licenseReport {
        outputDir = "$projectDir/build/reports/dependency-license"
        projects = [project] + project.subprojects
        configurations = ['testCompileClasspath', 'testRuntimeClasspath']
        excludeGroups = [group]
        excludes = ['moduleGroup:moduleName']
        excludeOwnGroup = true
        excludeBoms = false
        renderers = [new XmlReportRenderer('third-party-libs.xml', 'Back-End Libraries')]
        renderers += [new InventoryHtmlReportRenderer('third-party-libs.html', 'Back-End Libraries')]
        renderers += [new SimpleHtmlReportRenderer('third-party-libs-simple.html')]
        renderers += [new JsonReportRenderer('third-party-libs.json')]
        importers = [new XmlReportImporter("For Test", new File("$rootProject.projectDir/gradle/license", "externalDependencies.xml"))]
        filters = [new LicenseBundleNormalizer()]
        allowedLicensesFile = new File("$rootProject.projectDir/gradle/license/allowed-licenses.json")
    }
}
ext {
    gitRevision = "git rev-parse HEAD".execute().text.trim()
    gitBranch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
    gitUrl = "git remote get-url --push origin".execute().text.trim()
    gitUserEmail = "git config user.email".execute().text.trim()
    gitUserName = "git config user.name".execute().text.trim()
    jdkVersion = JavaLanguageVersion.of(21)
}
subprojects {

    group = rootProject.group
    version = rootProject.version
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.gorylenko.gradle-git-properties'
    apply from: "$rootProject.projectDir/gradle/dependency-check/dependency-check.gradle"
    apply from: "$rootProject.projectDir/gradle/spotbugs/spotbugs.gradle"
    apply from: "$rootProject.projectDir/gradle/checkstyle/checkstyle.gradle"
    apply from: "$rootProject.projectDir/gradle/publish/publish-artifact.gradle"
    apply from: "${rootProject.projectDir}/gradle/version-info.gradle"
    apply from: "$rootProject.projectDir/gradle/sonarqube.gradle"
    apply from: "$rootProject.projectDir/gradle/dependency-check/dependency-check.gradle"
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = nexusUrl
        }
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = nexusUrl2
        }
    }
}

// 根目錄不需要包jar
bootJar.enabled = false
jar.enabled = false
