import org.apache.tools.ant.filters.ReplaceTokens

description = 'boot'

apply from: "$rootProject.projectDir/gradle/logging/logging.gradle"
apply from: "$rootProject.projectDir/gradle/deploy/deploy.gradle"

dependencies {
    // 引入web模組
    implementation project(':web')
    // 引入flowable模組
    implementation project(':flowable-process')
    // 引入BT內部模組
    implementation libs.bundles.blue.tech.bundle
    // 引入undertow
    implementation libs.bundles.undertow
}

def activeProfile = System.getProperty("env", project.hasProperty("profile") ? project.property("profile") : "local")

processResources {

    doFirst {
        println "Active Profile: ${activeProfile}"
        // 只針對application.properties進行替換
        filesMatching(["*.properties", "*.yml"]) { file ->
            println("Processing file: ${file.path}")
            // 在文件中替換特定的 tokens
            filter(ReplaceTokens, tokens: [
                    "spring.active.profiles": activeProfile,
                    "project.groupId"       : project.group,
                    "project.artifactId"    : project.name,
                    "project.version"       : project.version
            ])
        }
    }

    // 忽略重複的資源載入
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // 依照所需資源打包至 resources
    sourceSets {
        main {
            resources {
                // 過濾 resources
                include("application-actuator.yml")
                include("application-info.yml")
                include("application-undertow.yml")
                include("META-INF/*")
            }
        }
    }
    def environments = ['local']

    if (environments.contains(activeProfile)) {
        from "src/main/resources/config/${activeProfile}/application.yml"
        from "src/main/resources/config/${activeProfile}/application-eureka.yml"
        from "src/main/resources/config/${activeProfile}/adapter.yml"
        from "src/main/resources/log4j2.yml"
        into 'src/main/resources/'
    }
}


jar {
    enabled = false
}

bootRun {
    enabled = true
    mainClass.set('com.bot.flowable.BootApplication')
    jvmArgs += [
            "-Xms512m", // Minimum heap size
            "-Xmx2048m" // Maximum heap size
    ]
    def user_home = System.getProperty('user.home')
    // for logging 預設log放在user_home/app/fsap/log
    jvmArgs += ['-Dlog4j.skipJansi=false', '-DLOG4J2_DISABLE_ANSI=false', "-DBASE_PATH=${user_home}/app/fsap/log"]
    // log4j2 config path
    jvmArgs += ["-Dlogging.config=$projectDir/src/main/resources/log4j2.yml"]
}
